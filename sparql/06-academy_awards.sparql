SELECT ?awardLabel ?item ?itemLabel (GROUP_CONCAT(?winnerLabel; SEPARATOR = ", ") AS ?winners) ?work ?workLabel ?role ?roleLabel WHERE {
  ?item p:P166 ?awardStatement.
  ?awardStatement ps:P166 ?award;
    pq:P805 wd:Q47496499.
  OPTIONAL { ?awardStatement pq:P1346 ?winner. }
  OPTIONAL { ?awardStatement pq:P1686 ?work. }
  OPTIONAL { ?awardStatement pq:P453 ?role. }
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en".
    ?award rdfs:label ?awardLabel.
    ?item rdfs:label ?itemLabel.
    ?winner rdfs:label ?winnerLabel.
    ?work  rdfs:label ?workLabel.
    ?role  rdfs:label ?roleLabel.
  }
}
GROUP BY ?awardLabel ?item ?itemLabel ?work ?workLabel ?role ?roleLabel